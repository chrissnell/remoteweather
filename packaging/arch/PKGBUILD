# Maintainer: Chris Snell <chris.snell@gmail.com>
pkgname=remoteweather
pkgver=5.7.16
pkgrel=1
pkgdesc="Weather station data collection and distribution system with REST API, management API, and dynamic configuration"
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/chrissnell/remoteweather"
license=('Apache')
depends=('glibc' 'postgresql' 'timescaledb')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/chrissnell/remoteweather/archive/refs/tags/v$pkgver.tar.gz"
        "remoteweather.service"
        "remoteweather.sysusers")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

build() {
    cd "$srcdir/remoteweather-master"
    make clean
    rm -f campbell-emulator davis-emulator migrate remoteweather-timescaledb-provisioner
    rm -rf bin/
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=mod -modcacherw"
    export GOOS=linux
    case "$CARCH" in
        x86_64)   export GOARCH=amd64 ;;
        aarch64)  export GOARCH=arm64 ;;
        armv7h)   export GOARCH=arm   ;;
    esac
    make VERSION="v${pkgver}"
    go build -o campbell-emulator ./cmd/campbell-emulator/
    go build -o davis-emulator ./cmd/davis-emulator/
    go build -o migrate ./cmd/migrate/
    go build -o remoteweather-timescaledb-provisioner ./cmd/remoteweather-timescaledb-provisioner/
}

package() {
    cd "$srcdir/remoteweather-master"
    install -Dm755 bin/remoteweather "$pkgdir/usr/bin/remoteweather"
    install -Dm755 campbell-emulator "$pkgdir/usr/bin/remoteweather-campbell-emulator"
    install -Dm755 davis-emulator "$pkgdir/usr/bin/remoteweather-davis-emulator"
    install -Dm755 migrate "$pkgdir/usr/bin/remoteweather-migrate"
    install -Dm755 remoteweather-timescaledb-provisioner "$pkgdir/usr/bin/remoteweather-timescaledb-provisioner"
    install -Dm644 "$srcdir/remoteweather.service" "$pkgdir/usr/lib/systemd/system/remoteweather.service"
    install -Dm644 "$srcdir/remoteweather.sysusers" "$pkgdir/usr/lib/sysusers.d/remoteweather.conf"
    install -dm755 "$pkgdir/var/lib/remoteweather"
    install -dm755 "$pkgdir/etc/remoteweather"
    install -Dm644 README.md "$pkgdir/usr/share/doc/remoteweather/README.md"
    # Install migrations for both config (SQLite) and weather (TimescaleDB) databases
    install -dm755 "$pkgdir/usr/share/remoteweather/migrations/config"
    install -Dm644 migrations/config/*.sql "$pkgdir/usr/share/remoteweather/migrations/config/"
    install -dm755 "$pkgdir/usr/share/remoteweather/migrations/weather"
    if ls migrations/weather/*.sql 1> /dev/null 2>&1; then
        install -Dm644 migrations/weather/*.sql "$pkgdir/usr/share/remoteweather/migrations/weather/"
    fi
} 